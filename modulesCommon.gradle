apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'signing'
apply plugin: 'maven-publish'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

group = 'com.github.mindbox-cloud'

android {
    compileSdkVersion libs.versions.compile.sdk.get().toInteger()
    buildToolsVersion libs.versions.build.tools.get()

    defaultConfig {
        minSdkVersion libs.versions.min.sdk.get().toInteger()
        targetSdkVersion libs.versions.target.sdk.get().toInteger()
        versionCode 1
        multiDexEnabled true
        versionName '"' + SDK_VERSION_NAME + '"'

        buildConfigField 'String', 'VERSION_NAME', '"' + SDK_VERSION_NAME + '"'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            debuggable true
            buildConfigField "Boolean", "DEBUG_MODE", "true"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

/*tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}*/

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'cloud.mindbox'
                artifactId = ARTIFACT_ID
                version = SDK_VERSION_NAME
                pom {
                    name = ARTIFACT_NAME
                    description = 'Android Mindbox SDK'
                    url = 'https://github.com/mindbox-cloud/android-sdk'
                    licenses {
                        license {
                            name = 'The Mindbox License'
                            url = 'https://github.com/mindbox-cloud/android-sdk/blob/master/LICENSE.md'
                        }
                    }

                    developers {
                        developer {
                            id = 'Mindbox'
                            name = 'Mindbox'
                            email = 'android-sdk@mindbox.ru'
                        }
                    }

                    scm {
                        connection = 'scm:https://github.com/mindbox-cloud/android-sdk.git'
                        developerConnection = 'scm:git://github.com/mindbox-cloud/android-sdk.git'
                        url = 'https://github.com/mindbox-cloud/android-sdk'
                    }
                }
            }
        }

        repositories {
            if (!project.hasProperty("sonatypeUsername")) {
                ext.sonatypeUsername = "sonatypeUsername"
            }
            if (!project.hasProperty("sonatypePassword")) {
                ext.sonatypePassword = "sonatypePassword"
            }
            maven {
                name = "OSSRH"
                url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2")
                credentials {
                    username = sonatypeUsername
                    password = sonatypePassword
                }
            }
        }
    }

    signing {
        def signingKeyId = findProperty("signing.keyId")
        def signingKey = findProperty("signing.secretKeyRingFile")
        def signingPassword = findProperty("signing.password")
        if (signingKeyId && signingKey && signingPassword) {
            println("Signing applied")
            sign publishing.publications.release
        } else {
            println("Signing not applied")
        }
    }
}