apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'signing'
apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'com.vanniktech.maven.publish'

group = 'com.github.mindbox-cloud'

android {
    compileSdkVersion libs.versions.compile.sdk.get().toInteger()
    buildToolsVersion libs.versions.build.tools.get()

    defaultConfig {
        minSdkVersion libs.versions.min.sdk.get().toInteger()
        targetSdkVersion libs.versions.target.sdk.get().toInteger()
        versionCode 1
        multiDexEnabled true
        versionName '"' + SDK_VERSION_NAME + '"'

        buildConfigField 'String', 'VERSION_NAME', '"' + SDK_VERSION_NAME + '"'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            debuggable true
            buildConfigField "Boolean", "DEBUG_MODE", "true"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    kotlin {
        explicitApi()
    }
}

mavenPublishing {
    publishToMavenCentral("CENTRAL_PORTAL")

    if ((System.getenv("CI") == "true")) {
        signAllPublications()
    }

    coordinates('cloud.mindbox', ARTIFACT_ID, SDK_VERSION_NAME)

    pom {
        name = ARTIFACT_NAME
        description = 'Android Mindbox SDK'
        url = 'https://github.com/mindbox-cloud/android-sdk'
        licenses {
            license {
                name = 'The Mindbox License'
                url = 'https://github.com/mindbox-cloud/android-sdk/blob/master/LICENSE.md'
            }
        }

        developers {
            developer {
                id = 'Mindbox'
                name = 'Mindbox'
                email = 'android-sdk@mindbox.ru'
            }
        }

        scm {
            connection = 'scm:https://github.com/mindbox-cloud/android-sdk.git'
            developerConnection = 'scm:git://github.com/mindbox-cloud/android-sdk.git'
            url = 'https://github.com/mindbox-cloud/android-sdk'
        }
    }
}