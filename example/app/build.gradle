plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'com.huawei.agconnect'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace 'com.mindbox.example'
    compileSdk 35

    defaultConfig {
        applicationId "com.mindbox.example"
        minSdk 21
        targetSdk 35
        versionCode 7
        versionName "2.5"
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    viewBinding {
        enabled = true
    }
    buildFeatures {
        buildConfig = true
    }
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        release {
            if (keystorePropertiesFile.exists()) {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }

}
// If the version of firebase-bom used in the sdk is higher, then it will be applied
// To change the dependency resolution strategy use the code below
/* configurations.configureEach {
    resolutionStrategy {
        force 'com.google.firebase:firebase-bom:32.7.1'
    }
} */
dependencies {

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    //Push services
    implementation platform('com.google.firebase:firebase-bom:32.7.1')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.huawei.hms:push:6.11.0.300'
    implementation 'ru.rustore.sdk:pushclient:6.10.0'

    implementation 'com.google.code.gson:gson:2.11.0'

    //Mindbox
    implementation 'cloud.mindbox:mobile-sdk:2.14.2'
    implementation 'cloud.mindbox:mindbox-firebase'
    implementation 'cloud.mindbox:mindbox-huawei'
    implementation 'cloud.mindbox:mindbox-rustore'

    //Glade for custom loader
    implementation 'com.github.bumptech.glide:glide:4.15.1'
    implementation 'androidx.activity:activity-ktx:1.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.15.1'

    // Multidex
    implementation "androidx.multidex:multidex:2.0.1"
}
