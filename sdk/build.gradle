plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.dokka'
    id 'maven'
    id 'signing'
}
apply plugin: 'maven-publish'
group = 'com.github.mindbox-moscow'
apply plugin: 'com.github.dcendents.android-maven'


android {
    compileSdkVersion 31
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 31
        versionCode 1
        multiDexEnabled true
        versionName '"' + SDK_VERSION_NAME + '"'

        buildConfigField 'String', 'VERSION_NAME', '"' + SDK_VERSION_NAME + '"'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    dokka {
        outputFormat = 'javadoc'
        outputDirectory = "$buildDir/dokka"
    }
    task androidSourcesJar(type: Jar) {
        archiveClassifier.set('sources')
        from android.sourceSets.main.java.srcDirs
    }

    afterEvaluate {
        publishing {
            publications {
                release(MavenPublication) {
                    from components.release
                    artifact androidSourcesJar
                    groupId = 'cloud.mindbox'
                    artifactId = 'mobile-sdk'
                    version = SDK_VERSION_NAME
                    pom {
                        name = 'Mindbox Android SDK'
                        description = 'Test release'
                        url = 'https://github.com/mindbox-moscow/android-sdk'
                        licenses {
                            license {
                                name = 'The Mindbox License'
                                url = 'https://github.com/mindbox-moscow/android-sdk/blob/master/LICENSE.md'
                            }
                        }
                        developers {
                            developer {
                                id = 'mindbox'
                                name = 'Petr Nikitin'
                                email = 'nikitin@mindbox.ru'
                            }
                        }
                        scm {
                            connection = 'scm:https://github.com/mindbox-moscow/android-sdk.git'
                            developerConnection = 'scm:git://github.com/mindbox-moscow/android-sdk.git'
                            url = 'https://github.com/mindbox-moscow/android-sdk'
                        }
                    }
                }
            }
            repositories {
                if (!project.hasProperty("sonatypeUsername")) {
                    ext.sonatypeUsername = "sonatypeUsername"
                }
                if (!project.hasProperty("sonatypePassword")) {
                    ext.sonatypePassword = "sonatypePassword"
                }
                maven {
                    name = "OSSRH"
                    url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2")
                    credentials {
                        username = sonatypeUsername
                        password = sonatypePassword
                    }
                }
            }
        }
        signing {
            sign publishing.publications.release
        }
    }
}
dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.4.20"
    implementation 'androidx.core:core-ktx:1.3.2'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // Firebase messaging
    implementation platform('com.google.firebase:firebase-bom:26.1.1')
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.android.gms:play-services-basement:17.5.0'

    // Ads
    implementation 'com.google.android.gms:play-services-ads-identifier:17.0.0'

    // coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2'

    //network
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'com.google.code.gson:gson:2.8.6'

    // DB
    implementation 'androidx.room:room-runtime:2.3.0'
    kapt "androidx.room:room-compiler:2.3.0"

    //work manager
    implementation "androidx.work:work-runtime-ktx:2.7.0"

    // Handle app lifecycle
    implementation "androidx.lifecycle:lifecycle-process:2.3.1"

}